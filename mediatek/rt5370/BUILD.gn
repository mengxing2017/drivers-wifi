# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//packages/package.gni")

driver_module("rt5370_driver") {
  output_name = "rt5370"

  sources = [
    "binding.c",
    "device.cpp",
    "device.h",
    "driver.cpp",
    "register.h",
    "rt5370.h",
  ]

  defines = [ "MX_DEBUGLEVEL=2" ]

  deps = [
    "//drivers/wifi/common",
    "//magenta/system/ulib/ddktl",
    "//magenta/system/ulib/mx",
    "//magenta/system/ulib/mxtl",
  ]

  libs = [
    "ddk",
    "driver",
    "sync",
  ]
}

copy("rt5370-firmware") {
  sources = [
    "firmware/rt2870.bin",
  ]
  outputs = [
    "$root_out_dir/firmware/rt2870.bin",
  ]
}

package("rt5370") {
  deps = [
    ":rt5370_driver",

    # The copy step has to be in the shared-library toolchain to use the
    # expected output directory, since we treat firmware blobs as if they
    # were libraries which always use the shared-library toolchain to build.
    ":rt5370-firmware($shlib_toolchain)",
  ]

  drivers = [ {
        name = "rt5370.so"
      } ]

  libraries = [ {
        name = "firmware/rt2870.bin"
      } ]
}
